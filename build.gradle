buildscript {
	ext {
		dependencyManagementPluginVersion = '0.5.3.RELEASE'
        springBootVersion = '1.4.0.RELEASE'
    }
	
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath("io.spring.gradle:dependency-management-plugin:${dependencyManagementPluginVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

allprojects  {
	group='net.neatsoft'
	version = '1.0'

	apply plugin: 'idea'
	apply plugin: 'eclipse'

	idea {
		module {
			downloadJavadoc = true
			downloadSources = true
		}
	}

	repositories {
		maven { url 'https://repo.spring.io/libs-snapshot' }
		mavenLocal()
		mavenCentral()
		jcenter() 
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: "io.spring.dependency-management"

	sourceCompatibility = 1.8
    targetCompatibility = 1.8
    
	dependencyManagement {
		imports {
		    mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.SR3'
		}
	}

    dependencies {
		testCompile 'junit:junit:4.11'
		//testCompile 'org.springframework.boot:spring-boot-starter-test:${springBootVersion}'
		compile 'org.springframework.cloud:spring-cloud-starter-config'
		compile 'org.springframework.cloud:spring-cloud-starter-eureka'
    }

	task createJavaProject << {
		sourceSets*.java.srcDirs*.each { it.mkdirs() }
		sourceSets*.resources.srcDirs*.each { it.mkdirs()}
	}
}
